import re

configfile: "workflow/config/config.yaml"

DATASETS = config["datasets"]
REFINE_DATASETS = config.get("refine_datasets", DATASETS)

# Metrics configuration (optional)
METRICS_CFG = config.get("metrics", {})

DEFAULT_AV = {"Fundus-AVSeg", "leuven-haifa"}
AV_GT_DATASETS = set(METRICS_CFG.get("av_gt_datasets", [d for d in DEFAULT_AV if d in DATASETS]))
METRICS_DATASETS = METRICS_CFG.get("datasets", sorted(AV_GT_DATASETS))
METRICS_DIR = METRICS_CFG.get("out_dir", "results/metrics")

RESOLUTIONS = [str(r) for r in config.get("resolutions", ["576", "1024"])]
k_start, k_end = config.get("k_range", [3, 9])
k = range(int(k_start), int(k_end))
K_VALUES = list(k)  # For vascx features

# ---------------- GT targets (optional) ----------------
from pathlib import Path
from snakemake.io import glob_wildcards

GT_CFG = config.get("ground_truth", {})
GT_ENABLED = bool(GT_CFG.get("enabled", False))

# default to legacy_root if ground_truth.root not provided
GT_ROOT = Path(GT_CFG.get("root", config.get("legacy_root", "."))).resolve()

AV_CFG = ((GT_CFG.get("av_rgb", {}) or {}).get("datasets", {}) or {}) if GT_ENABLED else {}
VES_CFG = ((GT_CFG.get("vessel_gray", {}) or {}).get("datasets", {}) or {}) if GT_ENABLED else {}

# Datasets that have AV GTs and vessel-only GTs (from config)
AV_GT_DATASETS_ALL = sorted(AV_CFG.keys())
VESSEL_GT_DATASETS = sorted(VES_CFG.keys())

# Split AV datasets into those with and without {other_dir}
AV_GT_WITH_OTHERDIR = [
    d for d in AV_GT_DATASETS_ALL if "{other_dir}" in str(AV_CFG[d].get("pattern", ""))
]
AV_GT_SIMPLE = [d for d in AV_GT_DATASETS_ALL if d not in AV_GT_WITH_OTHERDIR]

# Discover other_dir values for datasets that use {other_dir}
# Apply exclusions from metrics.exclude_splits config
METRICS_EXCLUDE_SPLITS = METRICS_CFG.get("exclude_splits", {})

AV_OTHERDIRS = {}
for d in AV_GT_WITH_OTHERDIR:
    abs_pat = str(GT_ROOT / AV_CFG[d]["pattern"])
    ods, _samples = glob_wildcards(abs_pat)
    
    # Apply exclusions for this dataset
    excluded = METRICS_EXCLUDE_SPLITS.get(d, [])
    #if excluded:
    #  print(f"Excluding splits for {d} (from config): {excluded}")
    
    filtered_ods = [od for od in set(ods) if od not in excluded]
    AV_OTHERDIRS[d] = sorted(filtered_ods)

# Pre-build targets for other_dir datasets (since expand() doesn't do per-dataset lists nicely)
GT_AV_OTHERDIR_TARGETS = [
    f"data/{d}/{od}/downsampled/{res}px/GTs"
    for d in AV_GT_WITH_OTHERDIR
    for od in AV_OTHERDIRS.get(d, [])
    for res in RESOLUTIONS
]

# --- Metrics targets ---
METRICS_AV_WITH_OTHERDIR = [d for d in METRICS_DATASETS if d in AV_GT_WITH_OTHERDIR]
METRICS_AV_SIMPLE = [d for d in METRICS_DATASETS if d not in METRICS_AV_WITH_OTHERDIR]

METRICS_AV_OTHERDIR_TARGETS = [
    f"{METRICS_DIR}/{d}/{od}/metrics_{res}.csv"
    for d in METRICS_AV_WITH_OTHERDIR
    for od in AV_OTHERDIRS.get(d, [])
    for res in ["1024", "576"]
]

# Split vessel-only datasets into those with and without {other_dir}
VES_GT_WITH_OTHERDIR = [
    d for d in VESSEL_GT_DATASETS if "{other_dir}" in str(VES_CFG[d].get("pattern", ""))
]
VES_GT_SIMPLE = [d for d in VESSEL_GT_DATASETS if d not in VES_GT_WITH_OTHERDIR]

# Discover vessel other_dir values
VES_OTHERDIRS = {}
for d in VES_GT_WITH_OTHERDIR:
    abs_pat = str(GT_ROOT / VES_CFG[d]["pattern"])
    ods, _samples = glob_wildcards(abs_pat)
    
    # Apply exclusions for this dataset
    excluded = METRICS_EXCLUDE_SPLITS.get(d, [])
    #if excluded:
    #   print(f"Excluding vessel splits for {d} (from config): {excluded}")
    
    filtered_ods = [od for od in set(ods) if od not in excluded]
    VES_OTHERDIRS[d] = sorted(filtered_ods)

VES_GT_OTHERDIR_TARGETS = [
    f"data/{d}/{od}/downsampled/{res}px/GTs_vessel"
    for d in VES_GT_WITH_OTHERDIR
    for od in VES_OTHERDIRS.get(d, [])
    for res in RESOLUTIONS
]

# ---------------- Dataset structure discovery for vascx features ----------------
# Discover which datasets have other_dir structure (train/test/val splits)
# by checking if they have {other_dir} in their GT pattern OR by detecting subdirs

# Start with datasets that have {other_dir} in GT patterns
VASCX_DATASETS_WITH_OTHERDIR = set(AV_GT_WITH_OTHERDIR + VES_GT_WITH_OTHERDIR)

# For datasets without GT patterns, check legacy_root structure
for d in DATASETS:
    if d in VASCX_DATASETS_WITH_OTHERDIR:
        continue
    # Check if dataset has subdirs that look like splits (train/test/val)
    legacy_path = Path(config.get("legacy_root", ".")) / d
    if legacy_path.exists():
        subdirs = [x.name for x in legacy_path.iterdir() if x.is_dir()]
        # If it has common split names, treat as other_dir dataset
        if any(s in subdirs for s in ["train", "test", "val", "training", "testing", "validation", "fundus", "fundus_reco"]):
            VASCX_DATASETS_WITH_OTHERDIR.add(d)

SIMPLE_DATASETS = sorted([d for d in DATASETS if d not in VASCX_DATASETS_WITH_OTHERDIR])
OTHERDIR_DATASETS = sorted([d for d in DATASETS if d in VASCX_DATASETS_WITH_OTHERDIR])

# Build OTHERDIRS dict (combine from GT discoveries and apply exclusions)
OTHERDIRS = {}
for d in OTHERDIR_DATASETS:
    if d in AV_OTHERDIRS:
        OTHERDIRS[d] = AV_OTHERDIRS[d]  # Already filtered
    elif d in VES_OTHERDIRS:
        OTHERDIRS[d] = VES_OTHERDIRS[d]  # Already filtered
    else:
        # Discover from legacy_root
        legacy_path = Path(config.get("legacy_root", ".")) / d
        if legacy_path.exists():
            subdirs = sorted([x.name for x in legacy_path.iterdir() if x.is_dir() and (x / "seg_legacy").exists()])
            # Apply exclusions
            excluded = METRICS_EXCLUDE_SPLITS.get(d, [])
            filtered_subdirs = [s for s in subdirs if s not in excluded]
            OTHERDIRS[d] = filtered_subdirs


# Prevent dataset wildcards from accidentally matching paths like "refined/Fundus-AVSeg/k3"
# by constraining them to only configured dataset names.
dataset_regex = "(" + "|".join(re.escape(d) for d in DATASETS) + ")"
res_regex = "(" + "|".join(re.escape(str(r)) for r in RESOLUTIONS) + ")"
wildcard_constraints:
    dataset = dataset_regex,
    res = res_regex,
    k = r"\d+",
    kind = r"(segs_converted|roi_masks_binarized)"



# Rules to include
include: "./rules/vascxgray_to_rgb.smk"
include: "./rules/copy_meta.smk"
include: "./rules/filter_meta.smk"
#include: "./rules/create_roi_masks.smk"
include: "./rules/create_roi_masks_from_rgb.smk"
include: "./rules/binarize_masks.smk"
include: "./rules/downsample.smk"
include: "./rules/filter_downsampled_dirs.smk"
include: "./rules/refinement.smk"
include: "./rules/dice.smk"
include: "./rules/gt_conversion.smk"
include: "./rules/refined_rgb_to_labels.smk"
include: "./rules/vascx_dataset_from_refined.smk"
include: "./rules/vascx_features_from_refined.smk"
include: "./rules/vascx_features_from_unrefined.smk"
include: "./rules/aggregate_vascx_features.smk"
include: "./rules/connectivity_vascx.smk"


# Rule order
ruleorder: vascx_features_refined_otherdir > vascx_features_refined_simple
ruleorder: vascx_features_unrefined_otherdir > vascx_features_unrefined_simple
ruleorder: aggregate_vascx_otherdir > aggregate_vascx_simple
ruleorder: make_vascx_view_otherdir > make_vascx_view_simple
ruleorder: make_vascx_view_unrefined_otherdir > make_vascx_view_unrefined_simple
# Connectivity metrics rule order
ruleorder: connectivity_refined_otherdir > connectivity_refined_simple
ruleorder: connectivity_unrefined_otherdir > connectivity_unrefined_simple
ruleorder: aggregate_connectivity_refined_otherdir > aggregate_connectivity_refined_simple
ruleorder: aggregate_connectivity_unrefined_otherdir > aggregate_connectivity_unrefined_simple
ruleorder: compare_connectivity_otherdir > compare_connectivity_simple
ruleorder: plot_connectivity_refined_otherdir > plot_connectivity_refined_simple

VASCX_REFINED = bool(config.get("vascx", {}).get("refined_features_enabled", True))

if VASCX_REFINED:
    VASCX_REFINED_TARGETS = []

    # Simple datasets
    VASCX_REFINED_TARGETS += expand(
        rules.vascx_features_refined_simple.output.features,
        dataset=SIMPLE_DATASETS,
        k=K_VALUES,
        res=RESOLUTIONS,
    )

    # Other-dir datasets (build strings because expand() can't handle per-dataset other_dir lists cleanly)
    VASCX_REFINED_TARGETS += [
        f"{config.get('vascx', {}).get('features_refined_out', 'results/vascx_features_refined')}/{d}/{od}/k{k}/downsampled/{res}px/vascx_features.tsv"
        for d in OTHERDIR_DATASETS
        for od in OTHERDIRS.get(d, [])
        for k in K_VALUES
        for res in RESOLUTIONS
    ]
else:
    VASCX_REFINED_TARGETS = []

VASCX_UNREFINED = bool(config.get("vascx", {}).get("unrefined_features_enabled", False))

if VASCX_UNREFINED:
    VASCX_UNREFINED_TARGETS = []
    
    # Simple datasets
    VASCX_UNREFINED_TARGETS += expand(
        rules.vascx_features_unrefined_simple.output.features,
        dataset=SIMPLE_DATASETS,
        res=RESOLUTIONS,
    )
    
    # Other-dir datasets
    VASCX_UNREFINED_TARGETS += [
        f"{config.get('vascx', {}).get('features_unrefined_out', 'results/vascx_features_unrefined')}/{d}/{od}/downsampled/{res}px/vascx_features.tsv"
        for d in OTHERDIR_DATASETS
        for od in OTHERDIRS.get(d, [])
        for res in RESOLUTIONS
    ]
else:
    VASCX_UNREFINED_TARGETS = []

# VascX aggregation (optional)
VASCX_AGGREGATED = bool(config.get("vascx", {}).get("aggregation_enabled", False))

if VASCX_AGGREGATED:
    VASCX_AGGREGATED_TARGETS = []
    
    # Simple datasets
    VASCX_AGGREGATED_TARGETS += expand(
        rules.aggregate_vascx_simple.output.aggregated,
        dataset=SIMPLE_DATASETS,
        res=RESOLUTIONS,
    )
    
    # Other-dir datasets
    VASCX_AGGREGATED_TARGETS += [
        f"{config.get('vascx', {}).get('aggregated_out', 'results/vascx_aggregated')}/{d}/{od}/downsampled/{res}px/vascx_features_aggregated.tsv"
        for d in OTHERDIR_DATASETS
        for od in OTHERDIRS.get(d, [])
        for res in RESOLUTIONS
    ]
else:
    VASCX_AGGREGATED_TARGETS = []

CONNECTIVITY_REFINED = bool(config.get("vascx", {}).get("connectivity_refined_enabled", False))
CONNECTIVITY_UNREFINED = bool(config.get("vascx", {}).get("connectivity_unrefined_enabled", False))
CONNECTIVITY_COMPARISON = bool(config.get("vascx", {}).get("connectivity_comparison_enabled", False))

CONNECTIVITY_OUT = config.get("vascx", {}).get("connectivity_out", "results/connectivity_metrics")

if CONNECTIVITY_REFINED:
    CONNECTIVITY_REFINED_TARGETS = []
    
    # Simple datasets - summary files (aggregated metrics)
    CONNECTIVITY_REFINED_TARGETS += expand(
        f"{CONNECTIVITY_OUT}/refined/{{dataset}}/k{{k}}/downsampled/{{res}}px/summary.csv",
        dataset=SIMPLE_DATASETS,
        k=K_VALUES,
        res=RESOLUTIONS,
    )
    
    # Other-dir datasets - summary files
    CONNECTIVITY_REFINED_TARGETS += [
        f"{CONNECTIVITY_OUT}/refined/{d}/{od}/k{k}/downsampled/{res}px/summary.csv"
        for d in OTHERDIR_DATASETS
        for od in OTHERDIRS.get(d, [])
        for k in K_VALUES
        for res in RESOLUTIONS
    ]
else:
    CONNECTIVITY_REFINED_TARGETS = []

if CONNECTIVITY_UNREFINED:
    CONNECTIVITY_UNREFINED_TARGETS = []
    
    # Simple datasets - summary files
    CONNECTIVITY_UNREFINED_TARGETS += expand(
        f"{CONNECTIVITY_OUT}/unrefined/{{dataset}}/downsampled/{{res}}px/summary.csv",
        dataset=SIMPLE_DATASETS,
        res=RESOLUTIONS,
    )
    
    # Other-dir datasets - summary files
    CONNECTIVITY_UNREFINED_TARGETS += [
        f"{CONNECTIVITY_OUT}/unrefined/{d}/{od}/downsampled/{res}px/summary.csv"
        for d in OTHERDIR_DATASETS
        for od in OTHERDIRS.get(d, [])
        for res in RESOLUTIONS
    ]
else:
    CONNECTIVITY_UNREFINED_TARGETS = []

if CONNECTIVITY_COMPARISON:
    CONNECTIVITY_COMPARISON_TARGETS = []
    
    # Simple datasets - comparison files
    CONNECTIVITY_COMPARISON_TARGETS += expand(
        f"{CONNECTIVITY_OUT}/comparison/{{dataset}}/k{{k}}/downsampled/{{res}}px/refinement_comparison.csv",
        dataset=SIMPLE_DATASETS,
        k=K_VALUES,
        res=RESOLUTIONS,
    )
    
    # Other-dir datasets - comparison files
    CONNECTIVITY_COMPARISON_TARGETS += [
        f"{CONNECTIVITY_OUT}/comparison/{d}/{od}/k{k}/downsampled/{res}px/refinement_comparison.csv"
        for d in OTHERDIR_DATASETS
        for od in OTHERDIRS.get(d, [])
        for k in K_VALUES
        for res in RESOLUTIONS
    ]
else:
    CONNECTIVITY_COMPARISON_TARGETS = []
rule all:
    input:
        # Ensure meta gets copied for all datasets
        expand("data/{dataset}/meta/meta.csv", dataset=DATASETS),

        # Final refinement outputs
        expand(
            rules.refinement.output.refined,
            dataset=REFINE_DATASETS,
            res=RESOLUTIONS,
            k=k,
        ),
        
        # Ground-truth outputs (optional; enabled via config['ground_truth']['enabled'])
        # A/V GT datasets without other_dir
        expand("data/{dataset}/downsampled/{res}px/GTs", dataset=AV_GT_SIMPLE, res=RESOLUTIONS),
        # A/V GT datasets with other_dir (targets built explicitly)
        GT_AV_OTHERDIR_TARGETS,
        # Vessel-only GT datasets (grayscale)
        # Vessel-only GT datasets without other_dir
        expand("data/{dataset}/downsampled/{res}px/GTs_vessel", dataset=VES_GT_SIMPLE, res=RESOLUTIONS),
        # Vessel-only GT datasets with other_dir
        VES_GT_OTHERDIR_TARGETS,


        expand(f"{METRICS_DIR}" + "/{dataset}/metrics_1024.csv", dataset=METRICS_AV_SIMPLE),
        expand(f"{METRICS_DIR}" + "/{dataset}/metrics_576.csv",  dataset=METRICS_AV_SIMPLE),
        METRICS_AV_OTHERDIR_TARGETS,


        expand(
            rules.refined_rgb_to_labels.output.out_dir,
            dataset=REFINE_DATASETS,
            res=RESOLUTIONS,
            k=k,
        ),

        VASCX_REFINED_TARGETS,
        VASCX_UNREFINED_TARGETS,

        VASCX_AGGREGATED_TARGETS,

        CONNECTIVITY_REFINED_TARGETS,
        CONNECTIVITY_UNREFINED_TARGETS,
        CONNECTIVITY_COMPARISON_TARGETS,

    log:
        errors="logs/all.err",
        output="logs/all.log"
